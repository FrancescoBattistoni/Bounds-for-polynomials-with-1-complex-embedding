/* Study of inequality 15G */

/*
      +'                    <= 8
      -' +
         -' +'
            2R^(1/4)

*/


  Q:=RationalField();
  R<x,y,z,t,g>:=PolynomialRing(Q,5);

  L:=(1-2*x*y*g+(x*y)^2)*(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*t*g+(z*t)^2);  /* times 2*(1-g^2)^(1/4)  */

/* Notice that this function is symmetrical, in the sense that L(x,y,z,t,g) = L(t,z,y,x,g) */


/* Notice that the function is maximized at either x = 0 or x = 1. We begin with the 0-boundary */

/* x = 0:
   (1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*t*g+(z*t)^2)*2*(1-g^2)^(1/4).

   This is maximized at either t = 0 or t = 1 */


/* x = 0, t = 0:
   (1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*2*(1-g^2)^(1/4). */

  L:=(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*2;  /* times (1-g^2)^(1/4) */

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(Derivative(L,g)*(1-g^2)*2-g*L);
  Ly:=Ty[2,1];
  Lz:=Tz[1,1];
  Lg:=Tg[2,1];

  T1:=Factorization(Resultant(Ly,Lg,g));
  T2:=Factorization(Resultant(Lz,Lg,g));
  res1g:=T1[4,1];
  res2g:=T2[4,1];

  T11:=Factorization(Resultant(res1g,res2g,y));
  /* All factors are strictly positivi in the open set (also the factor: z^8 - 640241/384912*z^6 + 586957/577368*z^4 - 100627/1154736*z^2 + 100/6561) */

/* x = 0, t = 0, y = 0: (1+2*z*g+z^2)*2*(1-g^2)^(1/4) maximized for z = 1 getting 2*(1+g)*2*(1-g^2)^(1/4) <= 5.76 */
/* x = 0, t = 0, y = 1: 2*(1+g)*(1-z)*(1+2*z*g+z^2)*2*(1-g^2)^(1/4) */

  L:=2*(1+g)*(1-z)*(1+2*z*g+z^2)*2;  /* times (1-g^2)^(1/4) */

  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(Derivative(L,g)*(1-g^2)*2-g*L);
  Lz:=Tz[2,1];
  Lg:=Tg[3,1];

  T1:=Factorization(Resultant(Lz,Lg,g));
  res1g:=T1[4,1];
  /* GP computations show that there is one stationary point where the value is <= 6.15 */
/* x = 0, t = 0, y = 1, z = 0: 2*(1+g)*2*(1-g^2)^(1/4) <= 5.76 */
/* x = 0, t = 0, y = 1, z = 1: NULL */
/* x = 0, t = 0, y = 1, g = 0: 2*(1-z)*(1+z^2)*2 <= 4 */
/* x = 0, t = 0, y = 1, g = 1: NULL */

/* x = 0, t = 0, z = 0: (1+2*y*g+y^2)*2*(1-g^2)^(1/4) maximized for y = 1, getting  2*(1+g)*2*(1-g^2)^(1/4) <= 5.76 */
/* x = 0, t = 0, z = 1: 2*(1+2*y*g+y^2)*(1-y)*(1+g)*2*(1-g^2)^(1/4) <= 6.15 (by symmetry with previous case y = 1) */

/* x = 0, t = 0, g = 0: (1+y^2)*(1-y*z)*(1+z^2)*2 <= (1+y)*(1-y*z)*(1+z)*2 <= 2*2 = 4 */
/* x = 0, t = 0, g = 1:  NULL */


/* x = 0, t = 1:
   (1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*g+z^2)*2*(1-g^2)^(1/4)  */

  L:=(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*g+z^2);  /* times 2*(1-g^2)^(1/4)  */

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Ly:=Ty[3,1];
  Lz:=Tz[1,1];
  Lg:=Tg[2,1];

  T:=Factorization(Resultant(Ly,Lz,g));
  res1g:=T[2,1];
  T:=Factorization(Resultant(Ly,Lg,g));
  res2g:=T[4,1];

  T:=Factorization(Resultant(res1g,res2g,z));
  resz:=T[4,1]*T[8,1];

  PrintFile("15G_Pari.gp","\{resz=");
  PrintFile("15G_Pari.gp",resz);
  PrintFile("15G_Pari.gp",";}\n\n");

  PrintFile("15G_Pari.gp","\{res1g=");
  PrintFile("15G_Pari.gp",res1g);
  PrintFile("15G_Pari.gp",";}\n\n");

  PrintFile("15G_Pari.gp","\{Ly=");
  PrintFile("15G_Pari.gp",Ly);
  PrintFile("15G_Pari.gp",";}\n\n");
  /* There is only one stationary point, giving a value for the function of 3.7 */


/* x = 0, t = 1, y = 0:
  (1+2*z*g+z^2)*(1-2*z*g+z^2)*2*(1-g^2)^(1/4). This is maximized at g = 0 giving
  (1+z^2)*(1+z^2)*2. Maximized at z = 1 giving the value 8 */


/* x = 0, t = 1, z = 0:
   (1+2*y*g+y^2)*2*(1-g^2)^(1/4). Maximized at y = 1 and g = 1/2 giving 3*sqrt(3)  */


/* x = 0, t = 1, y = 1:
   (1+g)*(1-z)*(1+2*z*g+z^2)*(1-2*z*g+z^2)*4*(1-g^2)^(1/4)  */

  L:=4*(1+g)*(1-z)*(1+2*z*g+z^2)*(1-2*z*g+z^2);  /* times (1-g^2)^(1/4)  */

  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Lz:=Tz[2,1];
  Lg:=Tg[3,1];

  T:=Factorization(Resultant(Lg,Lz,g));
  resg:=T[5,1];
  /* This has no roots in the interior */


/* x = 0, t = 1, y = 1, z = 0:
   (1+g)*4*(1-g^2)^(1/4)  <=   3*sqrt(3)   */


/* x = 0, t = 1, y = 1, z = 1:  the function is zero   */


/* x = 0, t = 1, y = 1, g = 0:
   (1-z)*(1+z^2)*(1+z^2)*4 <=  4*(1-z^4) <= 4. */


/* x = 0, t = 1, y = 1, g = 1: the function is zero */


/* x = 0, t = 1, z = 1:
   (1+2*y*g+y^2)*(1-y)*(1-g^2)*8*(1-g^2)^(1/4)  */

  L:=8*(1+2*y*g+y^2)*(1-y)*(1-g^2);   /* (1-g^2)^(1/4) */

  Ty:=Factorization(Derivative(L,y));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Ly:=Ty[3,1];
  Lg:=Tg[4,1];

  T:=Factorization(Resultant(Lg,Ly,g));
  /* This has no roots in the interior */

/* x = 0, t = 1, z = 1, g = 0:  (1+y^2)*(1-y)*8 <=  8      */


/* x = 0, t = 1, z = 1, g = 1:  the function is zero       */


/* x = 0, t = 1, g = 0:
   2*(1+y^2)*(1-y*z)*(1+z^2)*(1+z^2)  <=  2 *  (1+y)*(1-y*z)*(1+z)  *  (1+z^2)  <=  2 * 2 * 2  <=  8  */


/* x = 0, t = 1, g = 1: the function is zero */
/* This concludes the case x = 0 */


/* We study now the case x = 1 */


/* x = 1:
   (1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*t*g+(z*t)^2)*2*(1-g^2)^(1/4). This is maximized at either t = 0 or t = 1  */

/* x = 1, t = 0:
   (1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*2*(1-g^2)^(1/4)  */


  L:=(1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2);  /* Times 2*(1-g^2)^(1/4)  */

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Ly:=Ty[2,1];
  Lz:=Tz[1,1];
  Lg:=Tg[2,1];

  T:=Factorization(Resultant(Ly,Lz,g));
  res1g:=T[2,1];
  T:=Factorization(Resultant(Lz,Lg,g));
  res2g:=T[4,1];

  T:=Factorization(Resultant(res1g,res2g,z));
  resz:=T[4,1]*T[7,1];

  PrintFile("15G_Pari.gp","\{resz=");
  PrintFile("15G_Pari.gp",resz);
  PrintFile("15G_Pari.gp",";}\n\n");

  PrintFile("15G_Pari.gp","\{res1g=");
  PrintFile("15G_Pari.gp",res1g);
  PrintFile("15G_Pari.gp",";}\n\n");

  PrintFile("15G_Pari.gp","\{Lz=");
  PrintFile("15G_Pari.gp",Lz);
  PrintFile("15G_Pari.gp",";}\n\n");
  /* GP computations find one critical point, but the value of the function does not go beyond 3.7 */


/* x = 1, t = 0, y = 0:
   (1+2*z*g+z^2)*2*(1-g^2)^(1/4). Maximized at z = 1, gives
   4*(1+g)*(1-g^2)^(1/4). This is maximized at g = 2/3 giving the bound 5.76   */


/* x = 1, t = 0, y = 1:
   (1-g)*(1+g)*(1-z)*(1+2*z*g+z^2)*8*(1-g^2)^(1/4)  */

  L:=8*(1-g)*(1+g)*(1-z)*(1+2*z*g+z^2);  /* times (1-g^2)^(1/4) */

  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Lz:=Tz[3,1];
  Lg:=Tg[4,1];

  T:=Factorization(Resultant(Lg,Lz,g));
  /* This has no roots in the interior */


/* x = 1, t = 0, y = 1, z = 0:  (1-g)*(1+g)*8*(1-g^2)^(1/4)  <=  8  */


/* x = 1, t = 0, y = 1, z = 1:  the function is zero  */


/* x = 1, t = 0, y = 1, g = 0:  (1-z)*(1+z^2)*8  <=   8 */


/* x = 1, t = 0, y = 1, g = 1:  the function is zero */


/* x = 1, t = 0, z = 0:
   (1-2*y*g+y^2)*(1+2*y*g+y^2)*2*(1-g^2)^(1/4). This is maximized at g = 0 giving
   (1+y^2)*(1+y^2)*2  <=  8  */


/* x = 1, t = 0, z = 1:
   (1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y)*(1+g)*4*(1-g^2)^(1/4)    */


  L:=4*(1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y)*(1+g);  /* times (1-g^2)^(1/4)  */

  Ty:=Factorization(Derivative(L,y));
  Tg:=Factorization(2*Derivative(L,g)*(1-g^2)-L*g);
  Ly:=Ty[2,1];
  Lg:=Tg[3,1];

  T:=Factorization(Resultant(Lg,Ly,g));
  resg:=T[5,1];
  /* No roots in the interior */


/* x = 1, t = 0, z = 1, g = 0:  (1-y^2)*(1+y^2)*(1-y)*4    <=  4*(1-y^4)  <=  4 */


/* x = 1, t = 0, z = 1, g = 1:  the function is zero */


/* x = 1, t = 0, g = 0:
   (1+y^2)*(1+y^2)*(1-y*z)*(1+z^2)*2   <=   2  *  (1+y)*(1-y*z)*(1+z)   *  (1+y^2)   <=  2 * 2 * 2   <=  8   */

/* x = 1, t = 0, g = 1: the function is zero */


/* x = 1, t = 1:
  (1-2*y*g+y^2)*(1+2*y*g+y^2)*(1-y*z)*(1+2*z*g+z^2)*(1-2*z*g+z^2)*2*(1-g^2)^(1/4). This is maximized at g = 0 giving
  2*(1+y^2)^2*(1-y*z)*(1+z^2)^2  */

  L:=2*(1+y^2)^2*(1-y*z)*(1+z^2)^2;

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Ly:=Ty[3,1];
  Lz:=Tz[2,1];

  T:=Factorization(Resultant(Lz,Ly,z));
  /* This is maximized at (y0,z0) with y0 = z0 = sqrt(3/5)  giving the value 5.25 */


/* x = 1, t = 1, y = 0:  2*(1+z^2)^2  <= 8  */

/* x = 1, t = 1, y = 1:  8*(1-z)*(1+z^2)^2 = 8*(1-z^4)  <=  8 */

/* x = 1, t = 1, z = 0:  2*(1+y^2)^2 <= 8 */

/* x = 1, t = 1, z = 1:  8*(1+y^2)^2*(1-y)    = 8*(1-y^4)  <=  8 */

/* This concludes the case x = 1 */


/* We don't need to study the interior thanks to the considerations about x.
   So the function is <= 8 */
