/* Study of inequality 6Gbis: */

/*
      - -' +                <= 14.6895   for : {t in [0,1    ] and y*z in [1/10,3/5]} OR
        +'                                   : {t in [0,22/25] and y*z in [3/5,1]}
           -' +'                         when the function is written as
                2R^(1/2)                 (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*t*g+(z*t)^2)*2*sqrt(1-g^2)
*/

Q:=RationalField();
R<x,y,z,t,g>:=PolynomialRing(Q,5);

L:=(1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*t*g+(z*t)^2);  /* times 2*sqrt(1-g^2) */

/* By convexity of (1-2*z*t*g+(z*t)^2), the maximum is achieved when t reaches its extremal values, so that:
when y*z in [1/10,3/5],  for t = 0  or t = 1,
when y*z in [3/5,1],     for t = 0  or t = 22/25.

The same set can be described as
CASE 1: t = 1      and y*z in [1/10,3/5],
CASE 2: t = 0      and y*z in [1/10,1],
CASE 3: t = 22/25  and y*z in [3/5,1].
*/

/* CASE 1: t = 1 and y*z in [1/10,3/5]
  (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*g+z^2)*2*sqrt(1-g^2).
  Factor (1+2*x*y*g+(x*y)^2)*(1-2*y*g+y^2) is maximized for g = 0, by RMK1.
  The same happens to factor (1+2*z*g+z^2)*(1-2*z*g+z^2) = (1+z^2)^2-4*z^2*g^2
  and to factor (1-g^2)^(1/2). Hence the function is maximized for g = 0.
  (1+x)*(1+(x*y)^2)*(1-x*y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2 */

  /* INNER */
  L:=(1+x)*(1+(x*y)^2)*(1-x*y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2;
  Tx:=Factorization(Derivative(L,x));
  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Lx:=Tx[3,1];
  Ly:=Ty[3,1];
  Lz:=Tz[4,1];

  T1:=Factorization(Resultant(Lx,Lz,z));
  T2:=Factorization(Resultant(Ly,Lz,z));
  res1z:=T1[3,1];
  res2z:=T2[4,1];

  T11:=Factorization(Resultant(res1z,res2z,y));
  res1y:=T11[3,1]*T11[4,1];

  PrintFile("6Gbis_Pari.gp","\{res1y=");
  PrintFile("6Gbis_Pari.gp",res1y);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1z=");
  PrintFile("6Gbis_Pari.gp",res1z);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Lx=");
  PrintFile("6Gbis_Pari.gp",Lx);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  /* GP computations show that there are no stationary points */

  /* BORDER */
/* CASE 1 (y*z in [1/10,3/5]), x = 0:
  (1+y^2)*(1+z^2)*(1+z^2)*2 */
  /* INNER */
  L:=(1+y^2)*(1+z^2)*(1+z^2)*2;
  /* L is strictly increasing in both variables: the maximum is at the border */

  /* BORDER */
  /* y in [1/10,3/5], z = 1: (1+y^2)*2*2*2 <= 10.88 */
  /* y in [3/5,1], z = 3/5/y: (1+y^2)*(1+z^2)*(1+z^2)*2 <= 10.88 */ /* PARI:  ploth(y=3/5,1,my(z=3/5/y);(1+y^2)*(1+z^2)*(1+z^2)*2) */
  /* y = 1, z in [1/10,3/5]: 2*(1+z^2)*(1+z^2)*2 <= 7.4 */
  /* y in [1/10,1], z = 1/10/y: (1+y^2)*(1+z^2)*(1+z^2)*2 <= 8.1 */ /* PARI:  ploth(y=1/10,1,my(z=1/10/y);(1+y^2)*(1+z^2)*(1+z^2)*2) */


/* CASE 1 (y*z in [1/10,3/5]), x = 1:
  2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2 */
  /* INNER */
  L:=2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2;
  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Ly:=Ty[3,1];
  Lz:=Tz[2,1];

  T1:=Factorization(Resultant(Ly,Lz,z));
  res1z:=T1[3,1];
  /* Unique stationary point: y^2 = z^2 = 3/5: this stationary point is on the border: not acceptable */

  /* BORDER */
  /* y in [1/10,3/5], z = 1: 2*(1+y^2)*(1-y)*(1+y^2)*2*2*2 <= 14.6895 */                  /* PARI: ploth(y=1/10,3/5,2*(1+y^2)*(1-y)*(1+y^2)*2*2*2) */
  /* y in [3/5,1], z = 3/5/y: 2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2 <= 11.84 */     /* PARI: ploth(y=3/5,1,my(z=3/5/y);2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2) */
  /* y = 1, z in [1/10,3/5]: 2*2*(1-z)*2*(1+z^2)*(1+z^2)*2 <= 14.6895 */                  /* PARI: ploth(z=1/10,3/5,2*2*(1-z)*2*(1+z^2)*(1+z^2)*2) */
  /* y in [1/10,1], z = 1/10/y: 2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2 <= 14.6895 */ /* PARI: ploth(y=1/10,1,my(z=1/10/y);2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*(1+z^2)*2) */


/* CASE 1 (y*z in [1/10,3/5]), z = 1, y in [1/10,3/5]:
  (1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2 */

  /* INNER */
  L:=(1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2;
  Tx:=Factorization(Derivative(L,x));
  Ty:=Factorization(Derivative(L,y));
  Lx:=Tx[2,1];
  Ly:=Ty[2,1];

  T1:=Factorization(Resultant(Lx,Ly,y));
  res1y:=T1[3,1];

  PrintFile("6Gbis_Pari.gp","\{res1y=");
  PrintFile("6Gbis_Pari.gp",res1y);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Ly=");
  PrintFile("6Gbis_Pari.gp",Ly);
  PrintFile("6Gbis_Pari.gp",";}\n\n");
  /* GP computations show that there are two stationary points with value <= 12.6 (see "6Gbis_Pari.gp") */

  /* BORDER */
  /* x = 0: (1+y^2)*2*2*2 max for y=3/5 <= 10.88 */
  /* x = 1: 2*(1+y^2)*(1-y)*(1+y^2)*2*2*2 <= 14.6895 */             /* PARI: ploth(y=1/10,3/5,2*(1+y^2)*(1-y)*(1+y^2)*2*2*2) */
  /* y = 1/10: (1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2 <= 14.6895*/ /* PARI: ploth(x=0,1,my(y=1/10);(1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2) */
  /* y = 3/5: (1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2 <= 12.63 */   /* PARI: ploth(x=0,1,my(y=3/5);(1+x)*(1+(x*y)^2)*(1-x*y)*(1+y^2)*2*2*2) */


/* CASE 1 (y*z in [1/10,3/5]), z = 3/5/y, y in [3/5,1]:
  (1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2 */

  /* INNER */
  /* computations are made in PARI because Magma has difficulty to derive fractions */
  /* GP computations show that there is only one stationary point where the value is <= 10.7 */

   /* BORDER */
  /* x = 0:   (1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2 <= 10.88 */ /* PARI ploth(y=3/5,1,my(x=0);(1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2) */
  /* x = 1:   (1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2 <= 11.84 */ /* PARI ploth(y=3/5,1,my(x=1);(1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2) */
  /* y = 3/5: (1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2 <= 12.63 */ /* PARI ploth(x=0,1,my(y=3/5);(1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2) */
  /* y = 1:   (1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2 <= 11.84 */ /* PARI ploth(x=0,1,my(y=1);(1+x)*(1+(x*y)^2)*(1-x*3/5)*(1+y^2)*(1+(3/5/y)^2)*(1+(3/5/y)^2)*2) */


/* CASE 1 (y*z in [1/10,3/5]), y = 1, z in [1/10,3/5]:
  (1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2 */
  /* INNER */
  L:=(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2;
  Tx:=Factorization(Derivative(L,x));
  Tz:=Factorization(Derivative(L,z));
  Lx:=Tx[2,1];
  Lz:=Tz[3,1];

  T1:=Factorization(Resultant(Lx,Lz,z));
  res1z:=T1[2,1];
  PrintFile("6Gbis_Pari.gp","\{res1z=");
  PrintFile("6Gbis_Pari.gp",res1z);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Lz=");
  PrintFile("6Gbis_Pari.gp",Lz);
  PrintFile("6Gbis_Pari.gp",";}\n\n");
  /* GP computations show that there is one stationary point with value <= 11 (see "6Gbis_Pari.gp") */

   /* BORDER */
  /* x = 0:   (1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2 <= 7.4     */ /* PARI ploth(z=1/10,3/5,my(x=0);(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2) */
  /* x = 1:   (1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2 <= 14.6895 */ /* PARI ploth(z=1/10,3/5,my(x=1);(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2) */
  /* z = 1/10:(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2 <= 14.6895 */ /* PARI ploth(x=0,1,  my(z=1/10);(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2) */
  /* z = 3/5: (1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2 <= 11.84   */ /* PARI ploth(x=0,1,  my(z=3/5) ;(1+x)*(1+x^2)*(1-x*z)*2*(1+z^2)*(1+z^2)*2) */


/* CASE 1 (y*z in [1/10,3/5]), z = 1/10/y, y in [1/10,1]:
  (1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2 */

  /* INNER */
  /* computations are made in PARI because Magma has difficulty to derive fractions */
  /* GP computations show that there are no stationary points */

   /* BORDER */
  /* x = 0:   (1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2 <= 8.08    */ /* PARI ploth(y=1/10,1,my(x=0)  ;(1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2) */
  /* x = 1:   (1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2 <= 14.6895 */ /* PARI ploth(y=1/10,1,my(x=1)  ;(1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2) */
  /* y = 1/10:(1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2 <= 14.6895 */ /* PARI ploth(x=0,1,  my(y=1/10);(1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2) */
  /* y = 1:   (1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2 <= 14.6895 */ /* PARI ploth(x=0,1,  my(y=1)   ;(1+x)*(1+(x*y)^2)*(1-x*1/10)*(1+y^2)*(1+(1/10/y)^2)*(1+(1/10/y)^2)*2) */
/* CASE 1 terminates here */





/* CASE 2: t = 0 and y*z in [1/10,1]:
  (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2) */

  /* INNER */
  L:=(1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2); /* 2*sqrt(1-g^2) */
  Tx:=Factorization(Derivative(L,x));
  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(Derivative(L,g)*(1-g^2)-g*L);
  Lx:=Tx[3,1];
  Ly:=Ty[3,1];
  Lz:=Tz[3,1];
  Lg:=Tg[3,1];

  T1:=Factorization(Resultant(Lx,Lz,z));
  T2:=Factorization(Resultant(Ly,Lz,z));
  T3:=Factorization(Resultant(Lg,Lz,z));
  res1z:=T1[3,1];
  res2z:=T2[3,1];
  res3z:=T3[4,1];

  T11:=Factorization(Resultant(res2z,res1z,y));
  T12:=Factorization(Resultant(res3z,res1z,y));
  res1y:=T11[5,1]*T11[6,1];
  res2y:=T12[5,1]*T12[6,1];

  T111:=Factorization(Resultant(res1y,res2y,g));
  res1g:=T111[4,1]*T111[5,1]*T111[6,1]*T111[7,1]*T111[8,1]*T111[9,1]*T111[10,1]*T111[11,1]*T111[12,1];

  PrintFile("6Gbis_Pari.gp","\{res1g=");
  PrintFile("6Gbis_Pari.gp",res1g);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1y=");
  PrintFile("6Gbis_Pari.gp",res1y);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1z=");
  PrintFile("6Gbis_Pari.gp",res1z);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Lz=");
  PrintFile("6Gbis_Pari.gp",Lz);
  PrintFile("6Gbis_Pari.gp",";}\n\n");
  /* GP computations show that there are 10 stationary points with value <= 7.1 */

  /* BORDER */
/* CASE 2: t = 0 for y*z in [1/10,1], x = 0:
  (1-2*y*g+y^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2): maximized for z = 1
  (1-2*y*g+y^2)*2*(1+g)*2*sqrt(1-g^2) maximized for y = 0 or y = 1
  max(2*(1+g)*2*sqrt(1-g^2), 2*(1-g)*2*(1+g)*2*sqrt(1-g^2)) <= 8 */

/* CASE 2: t = 0 for y*z in [1/10,1], x = 1:
  2*(1+2*y*g+y^2)*(1-y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2) */
  L:= 2*(1+2*y*g+y^2)*(1-y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2); /* 2*sqrt(1-g^2) */

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(Derivative(L,g)*(1-g^2)-g*L);
  Ly:=Ty[2,1];
  Lz:=Tz[1,1];
  Lg:=Tg[2,1];

  T1:=Factorization(Resultant(Ly,Lz,z));
  T2:=Factorization(Resultant(Lg,Lz,z));
  res1z:=T1[2,1];
  res2z:=T2[4,1];

  T11:=Factorization(Resultant(res2z,res1z,y));
  res1y:=T11[4,1]*T11[5,1];

  PrintFile("6Gbis_Pari.gp","\{res1y=");
  PrintFile("6Gbis_Pari.gp",res1y);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1z=");
  PrintFile("6Gbis_Pari.gp",res1z);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Lz=");
  PrintFile("6Gbis_Pari.gp",Lz);
  PrintFile("6Gbis_Pari.gp",";}\n\n");
  /* GP computations show that there are 3 stationary points with value <= 7.3 */

/* CASE 2: t = 0 for y*z in [1/10,1], x = 1, g = 0:
  2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*2 */
  L:=2*(1+y^2)*(1-y*z)*(1+y^2)*(1+z^2)*2;

  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Ly:=Ty[3,1];
  Lz:=Tz[1,1];

  T1:=Factorization(Resultant(Ly,Lz,z));
  res1z:=T1[3,1];
  /* y^2 = 7/25, it forces z = 0.37796447300922722721451653623418006082 or z = 0.88191710368819686350053858454642014190;
  so there are two stationary points (both with y*z in [1/10,1]) where the value is <= 6.3 */

/* CASE 2: t = 0 for y*z in [1/10,1], x = 1, g = 1:
  NULL */

/* CASE 2: t = 0 for y*z in [1/10,1], x = 1, border of y*z so:
  2*(1+2*y*g+y^2)*(1-y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2) */
/* CASE 2.1: t = 0 for y*z in [1/10,1], x = 1, z = 1 and y in [1/10,1]:
  2*(1+2*y*g+y^2)*(1-y)*(1-2*y*g+y^2)*2*(1+g)*2*sqrt(1-g^2) <= 10.4 (see GP computations in "6Gbis_Pari.gp") */
/* CASE 2.2: t = 0 for y*z in [1/10,1], x = 1, z = 1/10/y and y in [1/10,1]:
  2*(1+2*y*g+y^2)*(1-1/10)*(1-2*y*g+y^2)*(1+2*(1/10/y)*g+(1/10/y)^2)*2*sqrt(1-g^2) <= 14.64 (see GP computations in "6Gbis_Pari.gp") */
/* CASE 2.3: t = 0 for y*z in [1/10,1], x = 1, y = 1 and z in [1/10,1]:
  2*2*(1+g)*(1-z)*2*(1-g)*(1+2*z*g+z^2)*2*sqrt(1-g^2) <= 14.64 (see GP computations in "6Gbis_Pari.gp") */


/* CASE 2: t = 0 for y*z in [1/10,1], g = 0:
  (1+x)*(1+(x*y)^2)*(1-x*y*z)*(1+y^2)*(1+z^2)*2 <= 14.6 (see "6Gbis_Pari.gp") */

/* CASE 2: t = 0 for y*z in [1/10,1], g = 1:
  NULL */

/* CASE 2: t = 0 for y*z in [1/10,1], y*z border of [1/10,1]:
  (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2) <= 14.64 (see "6Gbis_Pari.gp") */
/* CASE 2 terminates here */





/* CASE 3: t = 22/25 and y*z in [3/5,1]:
  (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*22/25*g+(z*22/25)^2)*2*sqrt(1-g^2) */

  /* INNER */
  L:=(1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*22/25*g+(z*22/25)^2); /* 2*sqrt(1-g^2) */
  Tx:=Factorization(Derivative(L,x));
  Ty:=Factorization(Derivative(L,y));
  Tz:=Factorization(Derivative(L,z));
  Tg:=Factorization(Derivative(L,g)*(1-g^2)-g*L);
  Lx:=Tx[4,1];
  Ly:=Ty[4,1];
  Lz:=Tz[3,1];
  Lg:=Tg[3,1];

  T1:=Factorization(Resultant(Lx,Lz,x));
  T2:=Factorization(Resultant(Ly,Lz,x));
  T3:=Factorization(Resultant(Lg,Lz,x));
  res1x:=T1[3,1];
  res2x:=T2[3,1];
  res3x:=T3[3,1];

  T11:=Factorization(Resultant(res2x,res1x,z));
  T12:=Factorization(Resultant(res3x,res1x,z));
  res1z:=T11[5,1];
  res2z:=T12[5,1];

  T111:=Factorization(Resultant(res1z,res2z,g));
  res1g:=T111[5,1]*T111[6,1];


  PrintFile("6Gbis_Pari.gp","\{res1g=");
  PrintFile("6Gbis_Pari.gp",res1g);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1z=");
  PrintFile("6Gbis_Pari.gp",res1z);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{res1x=");
  PrintFile("6Gbis_Pari.gp",res1x);
  PrintFile("6Gbis_Pari.gp",";}\n\n");

  PrintFile("6Gbis_Pari.gp","\{Lz=");
  PrintFile("6Gbis_Pari.gp",Lz);
  PrintFile("6Gbis_Pari.gp",";}\n\n");
  /* GP computations show that there are no stationary points */

  /* BORDER */
/* CASE 3: t = 22/25 for y*z in [3/5,1], x = 0:
  (1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*22/25*g+(z*22/25)^2)*2*sqrt(1-g^2) <= 14.2 (see GP computations in "6Gbis_Pari.gp") */

/* CASE 3: t = 22/25 for y*z in [3/5,1], x = 1:
  2*(1+2*y*g+y^2)*(1-y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*22/25*g+(z*22/25)^2)*2*sqrt(1-g^2) <= 11.2 (see GP computations in "6Gbis_Pari.gp") */

/* CASE 3: t = 22/25 for y*z in [3/5,1], g = 0:
  (1+x)*(1+(x*y)^2)*(1-x*y*z)*(1+y^2)*(1+z^2)*(1+(z*22/25)^2)*2 <= 14.2 (see GP computations in "6Gbis_Pari.gp") */

/* CASE 3: t = 22/25 for y*z in [3/5,1], g = 1:
  NULL */

/* CASE 3: t = 22/25 for y*z in [3/5,1], y*z border of [3/5,1]:
  (1+x)*(1+2*x*y*g+(x*y)^2)*(1-x*y*z)*(1-2*y*g+y^2)*(1+2*z*g+z^2)*(1-2*z*22/25*g+(z*22/25)^2)*2*sqrt(1-g^2) <= 14.2 (see GP computations in "6Gbis_Pari.gp") */

/* CASE 3 terminates here */

/* The function is then <= 14.895 */
