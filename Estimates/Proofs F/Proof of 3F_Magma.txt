/* Study of inequality 3F */


Q:=RationalField();
R<x,y,z,t,g>:=PolynomialRing(Q,5);

L:=(1+x*y*z*t)*(1+2*y*z*g+(y*z)^2)*(1-y*z*t)*(1-2*z*g+z^2)*(1+z*t)*(1+2*t*g+t^2);  /* times 2*sqrt(1-g^2) */


/* This is maximized at x = 1 giving */


L:=(1-(y*z*t)^2)*(1+2*y*z*g+(y*z)^2)*(1-2*z*g+z^2)*(1+z*t)*(1+2*t*g+t^2);   /* times 2*sqrt(1-g^2) */


/* 0-boundaries */


/* y = 0:

   (1-2*z*g+z^2)*(1+z*t)*(1+2*t*g+t^2)*2*sqrt(1-g^2) <= 16 thanks to 2E  */


/* z = 0:

   (1+2*t*g+t^2)*2+sqrt(1-g^2)  <=  3*sqrt(3) thanks to 2B */


/* t = 0:

   (1+2*y*z*g+(y*z)^2)*(1-2*z*g+z^2)*2*sqrt(1-g^2). This is maximized at y = 1 and g = 0. giving 2*(1+z^2)^2 <=  8 */

/* g = 0:

   2*(1-(y*z*t)^2)*(1+(y*z)^2)*(1+z^2)*(1+z*t)*(1+t^2)  */

L:=2*(1-(y*z*t)^2)*(1+(y*z)^2)*(1+z^2)*(1+z*t)*(1+t^2);


Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));


Ly:=Ty[6,1];

Lz:=Tz[2,1];

Lt:=Tt[3,1];


T:=Factorization(Resultant(Ly,Lz,y));

/* This has no roots in the interior */


/* g = 0, t = 1:

   4*(1-(y*z)^4)*(1+z^2)*(1+z)  <=  16  */


/* g = 0, z = 1:

   4*(1-(y*t)^2)*(1+y^2)*(1+t)*(1+t^2)  =    4  *  (1-(y*t)^2)*(1+t^2)*(1+y^2)   *   (1+t)   <=   4*  2  (apply 9A)   * 2  =  16  */


/* g = 0, y = 1:

   2*(1-(z*t)^2)*(1+z^2)*(1+z^2)*(1+z*t)*(1+t^2)   <=     2*   (1-(z*t)^2)*(1+z^2)*(1+t^2)    *   (1+z^2)   *   (1+z*t)    <=   2   *     2  (apply 9A)   * 2   * 2  =  16  */


/* This concludes the 0-boundaries */


/*  g = 1:

    The function is zero  */


/* t = 1:

   (1-(y*z)^2)*(1+2*y*z*g+(y*z)^2)*(1-2*z*g+z^2)*(1+z)*(1+g)*4*sqrt(1-g^2)   */

L:=4*(1-(y*z)^2)*(1+2*y*z*g+(y*z)^2)*(1-2*z*g+z^2)*(1+z)*(1+g);  /* times sqrt(1-g^2)  */


Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Ly:=Ty[5,1];

Lz:=Tz[2,1];

Lg:=Tg[5,1];



T:=Factorization(Resultant(Ly,Lz,g));

res1g:=T[3,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[3,1];


T:=Factorization(Resultant(res1g,res2g,y));

resy:=T[4,1];

/* This has only one root in z: however, substitution in res1g gives no roots in y */


/* t = 1, z = 1:

   (1-y^2)*(1+2*y*g+(y)^2)*(1-g^2)*16*sqrt(1-g^2)

   The maximum of this function is in y0=1/sqrt(7) and g0=1/2/sqrt(7), giving the value 16M  */


/* t = 1, y = 1:

   (1-z^2)*(1+2*z*g+(y*z)^2)*(1-2*z*g+z^2)*(1+z)*(1+g)*4*sqrt(1-g^2)  */

L:=4*(1-z^2)*(1+2*z*g+(z)^2)*(1-2*z*g+z^2)*(1+z)*(1+g);  /* times sqrt(1-g^2)  */


Tz:=Factorization(Derivative(L,z));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[3,1];

Lg:=Tg[4,1];


T:=Factorization(Resultant(Lz,Lg,g));


resg:=T[5,1];

/* This has only one root

   z0=0.6628581961145969457283749618174615593884089345701171673438999077557637431800440513478909855437934125.

   Substitution in Lg gives a unique root in g

   g0=0.2720139388656168544838575334775825103721056524417507513935164206865339942751875500392117007170723922.

   The function evaluated in this point is < 8.9 */


/* z = 1 */


L:=4*(1-(y*t)^2)*(1+2*y*g+y^2)*(1-g)*(1+t)*(1+2*t*g+t^2);   /* times sqrt(1-g^2) */


Ty:=Factorization(Derivative(L,y));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Ly:=Ty[4,1];

Lt:=Tt[3,1];

Lg:=Tg[5,1];



T:=Factorization(Resultant(Ly,Lt,g));

res1g:=T[2,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[4,1];


T:=Factorization(Resultant(res1g,res2g,t));

rest:=T[4,1]*T[7,1];


PrintFile("3F_Pari.gp","\{rest=");
PrintFile("3F_Pari.gp",rest);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1g=");
PrintFile("3F_Pari.gp",res1g);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{Ly=");
PrintFile("3F_Pari.gp",Ly);
PrintFile("3F_Pari.gp",";}\n\n");

/* GP computations show there are no stationary points such that the value of the function at these points goes beyond 16M */


/* z = 1, y = 1:

   8*(1-t^2)*(1-g^2)*(1+t)*(1+2*t*g+t^2)*sqrt(1-g^2)   */

L:= 8*(1-t^2)*(1-g^2)*(1+t)*(1+2*t*g+t^2); /* times sqrt(1-g^2) */


Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lt:=Tt[4,1];

Lg:=Tg[5,1];


T:=Factorization(Resultant(Lt,Lg,g));

/* This has only one stationary point of coordinates

   t0=0.5622488487470864336892204302542748149870243137314678565505493185963018653726433990452024661397907169

   g0=0.2264055304669290210557627689089217593668901960821674103440933241226886658579021244032515413373691981

   the function at this point is < 12.5 */



/* y = 1 */


L:=(1-(z*t)^2)*(1+2*z*g+(z)^2)*(1-2*z*g+z^2)*(1+z*t)*(1+2*t*g+t^2);   /* times 2*sqrt(1-g^2) */


Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[3,1];

Lt:=Tt[4,1];

Lg:=Tg[3,1];



T:=Factorization(Resultant(Lz,Lt,g));

res1g:=T[3,1];

T:=Factorization(Resultant(Lt,Lg,g));

res2g:=T[5,1];


T:=Factorization(Resultant(res1g,res2g,t));

rest:=T[5,1]*T[6,1]*T[7,1];



PrintFile("3F_Pari.gp","\{rest=");
PrintFile("3F_Pari.gp",rest);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1g=");
PrintFile("3F_Pari.gp",res1g);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{Lt=");
PrintFile("3F_Pari.gp",Lt);
PrintFile("3F_Pari.gp",";}\n\n");


/* GP computations show there are no stationary points */


/* Interior */


L:=(1-(y*z*t)^2)*(1+2*y*z*g+(y*z)^2)*(1-2*z*g+z^2)*(1+z*t)*(1+2*t*g+t^2);   /* times 2*sqrt(1-g^2) */

Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Ly:=Ty[5,1];

Lz:=Tz[2,1];

Lt:=Tt[3,1];

Lg:=Tg[4,1];



T:=Factorization(Resultant(Ly,Lz,g));

res1g:=T[5,1];

T:=Factorization(Resultant(Ly,Lt,g));

res2g:=T[2,1];

T:=Factorization(Resultant(Ly,Lg,g));

res3g:=T[4,1];


T:=Factorization(Resultant(res1g,res2g,y));

res1ya:=T[2,1];

res1yb:=T[5,1];

T:=Factorization(Resultant(res1g,res3g,y));

res2ya:=T[2,1];

res2yb:=T[7,1];



/* We have res1ta = res2ta */


/* CASE 1: We are on the curve defined by res1ya=0:  t^2-1/3=0.

   We reduce the resultants in g modulo t^2-1/3=0 and we take the non-trivial factors */

res1gS:=y*z^2*t + 1/2*y*z + 9/8*z^2*t + 3/4*z + 3/8*t;


res2gS:=y^2*z^3 + 9/4*y^2*z^2*t + 3/8*y*z^2*t + 3/8*y*z - 9/8*z - 9/8*t;


res3gS:=y^4*z^5 + 9/14*y^3*z^5 + 3/7*y^3*z^4*t + 9/14*y^3*z^3 + 9/28*y^2*z^4*t -         39/14*y^2*z^3 + 9/28*y^2*z^2*t - 171/112*y*z^3 - 27/28*y*z^2*t -         171/112*y*z - 81/112*z^2*t + 27/28*z - 81/112*t;


T:=Factorization(Resultant(res1gS,res2gS,y));

res1Sy:=T[2,1];

T:=Factorization(Resultant(res1gS,res3gS,y));

res2Sy:=T[4,1];


T:=Factorization(Resultant(res1Sy,res2Sy,t));

rest:=T[5,1];


PrintFile("3F_Pari.gp","\{rest=");
PrintFile("3F_Pari.gp",rest);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1Sy=");
PrintFile("3F_Pari.gp",res1Sy);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1gS=");
PrintFile("3F_Pari.gp",res1gS);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{Ly=");
PrintFile("3F_Pari.gp",Ly);
PrintFile("3F_Pari.gp",";}\n\n");

/* GP computations found no stationary points */

/* CASE 2: We study the remaining resultants */


T:=Factorization(Resultant(res1yb,res2yb,t));

rest:=T[6,1]*T[7,1];


PrintFile("3F_Pari.gp","\{rest=");
PrintFile("3F_Pari.gp",rest);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1yb=");
PrintFile("3F_Pari.gp",res1yb);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{res1g=");
PrintFile("3F_Pari.gp",res1g);
PrintFile("3F_Pari.gp",";}\n\n");

PrintFile("3F_Pari.gp","\{Ly=");
PrintFile("3F_Pari.gp",Ly);
PrintFile("3F_Pari.gp",";}\n\n");

/* GP computations show there are no stationary points. This concludes the study of the interior */

/* Hence the function is <= 16M */
