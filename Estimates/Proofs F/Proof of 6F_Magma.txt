/* Study of inequality 6F */

Q:=RationalField();
P<z,y,t,a,g>:=PolynomialRing(Q,5);

L:=(1+y*z)*(1-2*y*z*t*g+(y*z*t)^2)*(1-y*z*t*a)*(1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t*a); /* times 2*sqrt(1-g^2) */


/* We begin with the 0-boundaries */

/* z = 0:

   2*sqrt(1-g^2)  <= 2  */


/* y = 0:

   (1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t*a)*2*sqrt(1-g^2). Maximized at t=a=1 giving

   (1-z^2)*(1+2*z*g+z^2)*2*sqrt(1-g^2)  */


L:=(1-z^2)*(1+2*z*g+z^2);  /* times 2*sqrt(1-g^2)  */


Tz:=Factorization(Derivative(L,z));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);

Lz:=Tz[1,1];

Lg:=Tg[3,1];


T:=Factorization(Resultant(Lz,Lg,g));

resg:=T[4,1];

/* We find the root z0=1/sqrt(5): substitution in Lz gives a g0 = 0.4472135954999579392818347337462552470881236719223051448541794490821041851275609798828828816757564550

   and the function at the point (z0,0,1,1,g0) has a value of 2.288.. < 64/27  */


/* Hence the case y = 0 is concluded looking at either z = 0 (already discussed) or z = 1 (the function is zero) */


/* t = 0:

   (1+y*z)*(1-z)*2*sqrt(1-g^2)  <=  2*(1-z^2)  <=  2  */


/* a = 0:

   (1+y*z)*(1-2*y*z*t*g+(y*z*t)^2)*(1-z)*(1+2*z*t*g+(z*t)^2)*2*sqrt(1-g^2)

   This is bounded by 64/27 applying 2*sqrt(1-g^2)<= 2 and then applying 10E  */


/* g = 0:

   2*(1+y*z)*(1+(y*z*t)^2)*(1-y*z*t*a)*(1-z)*(1+(z*t)^2)*(1+z*t*a)  */


L:=2*(1+y*z)*(1+(y*z*t)^2)*(1-y*z*t*a)*(1-z)*(1+(z*t)^2)*(1+z*t*a);

Tt:=Factorization(Derivative(L,t));

Ta:=Factorization(Derivative(L,a));


Lt:=Tt[4,1];

La:=Ta[5,1];


T:=Factorization(Resultant(Lt,La,a));

/* This has no roots in the interior */



/*  g = 0, z = 1

   the function is zero */


/*  g = 0, y = 1

   2*(1+z)*(1+(z*t)^2)*(1-z*t*a)*(1-z)*(1+(z*t)^2)*(1+z*t*a). This is maximized at a=0, so we are ok */


/*  g = 0, t = 1:

   2*(1+y*z)*(1+(y*z)^2)*(1-y*z*a)*(1-z)*(1+z^2)*(1+z*a)   */


L:=2*(1+y*z)*(1+(y*z)^2)*(1-y*z*a)*(1-z)*(1+z^2)*(1+z*a);


Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Ta:=Factorization(Derivative(L,a));


Lz:=Tz[1,1];

Ly:=Ty[5,1];

La:=Ta[4,1];



T:=Factorization(Resultant(Lz,La,a));

res1a:=T[4,1];

T:=Factorization(Resultant(Ly,La,a));

res2a:=T[1,1];



T:=Factorization(Resultant(res1a,res2a,z));

res1z:=T[4,1];

/* This has no roots between 0 and 1 */



/*  g = 0, t = 1, a = 1:

    2*(1+y*z)*(1+(y*z)^2)*(1-y*z)*(1-z)*(1+z^2)*(1+z)  =  2*(1-z^4)*(1-(y*z)^4)  <=  2   */


/*  g = 0, a = 1:

    2*(1+y*z)*(1+(y*z*t)^2)*(1-y*z*t)*(1-z)*(1+(z*t)^2)*(1+z*t)   */


L:=2*(1+y*z)*(1+(y*z*t)^2)*(1-y*z*t)*(1-z)*(1+(z*t)^2)*(1+z*t);

Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Tt:=Factorization(Derivative(L,t));


Lz:=Tz[1,1];

Ly:=Ty[5,1];

Lt:=Tt[4,1];



T:=Factorization(Resultant(Lz,Ly,y));

res1y:=T[5,1];

T:=Factorization(Resultant(Ly,Lt,y));

res2y:=T[3,1];



T:=Factorization(Resultant(res1y,res2y,z));

res1z:=T[5,1];


PrintFile("6F_Pari.gp","\{res1z=");
PrintFile("6F_Pari.gp",res1z);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{res1y=");
PrintFile("6F_Pari.gp",res1y);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{Ly=");
PrintFile("6F_Pari.gp",Ly);
PrintFile("6F_Pari.gp",";}\n\n");

/* We find no critical points. This concludes the case  g = 0 */


/* We now look at the 1-boundaries */

/* z = 1: the function is zero */

/* y = 1:

   (1+z)*(1-2*z*t*g+(z*t)^2)*(1-z*t*a)*(1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t*a)*2*sqrt(1-g^2).

   This is maximized at g = 0, so we know is <= 64/27 */


/* t = 1:

   (1+y*z)*(1-2*y*z*g+(y*z)^2)*(1-y*z*a)*(1-z)*(1+2*z*g+z^2)*(1+z*a)*2*sqrt(1-g^2)  */


L:=(1+y*z)*(1-2*y*z*g+(y*z)^2)*(1-y*z*a)*(1-z)*(1+2*z*g+z^2)*(1+z*a);  /* times 2*sqrt(1-g^2)  */


Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[1,1];

Ly:=Ty[5,1];

La:=Ta[4,1];

Lg:=Tg[5,1];



T:=Factorization(Resultant(Lz,La,a));

res1a:=T[4,1];

T:=Factorization(Resultant(Ly,La,a));

res2a:=T[1,1];

T:=Factorization(Resultant(Lg,La,a));

res3a:=T[1,1];


T:=Factorization(Resultant(res1a,res2a,g));

res1g:=T[6,1];

T:=Factorization(Resultant(res3a,res2a,g));

res2g:=T[6,1];


T:=Factorization(Resultant(res1g,res2g,y));

resy:=T[5,1];


PrintFile("6F_Pari.gp","\{resy=");
PrintFile("6F_Pari.gp",resy);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{res1g=");
PrintFile("6F_Pari.gp",res1g);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{res2a=");
PrintFile("6F_Pari.gp",res2a);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{La=");
PrintFile("6F_Pari.gp",La);
PrintFile("6F_Pari.gp",";}\n\n");


/* GP computations find two stationary points such that the value of the function at these is < 2.2 < 64/27 */


/* t = 1, a = 1:

   (1+y*z)*(1-2*y*z*g+(y*z)^2)*(1-y*z)*(1-z)*(1+2*z*g+z^2)*(1+z)*2*sqrt(1-g^2)  */


L:=(1+y*z)*(1-2*y*z*g+(y*z)^2)*(1-y*z)*(1-z)*(1+2*z*g+z^2)*(1+z); /* times 2*sqrt(1-g^2) */

Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[1,1];

Ly:=Ty[5,1];

Lg:=Tg[5,1];




T:=Factorization(Resultant(Lz,Ly,g));

res1g:=T[5,1];

T:=Factorization(Resultant(Lg,Ly,g));

res2g:=T[4,1];


T:=Factorization(Resultant(res1g,res2g,z));

/* No need to check t = 1 with g = 1 since the function will be zero */

/* The only acceptable root is y=1/2. But substitution in res1g find no acceptable roots in z */


/* a = 1:

   (1+y*z)*(1-2*y*z*t*g+(y*z*t)^2)*(1-y*z*t)*(1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t)*2*sqrt(1-g^2)  */


L:=(1+y*z)*(1-2*y*z*t*g+(y*z*t)^2)*(1-y*z*t)*(1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t);  /* times 2*sqrt(1-g^2) */



Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[1,1];

Ly:=Ty[5,1];

Lt:=Tt[4,1];

Lg:=Tg[5,1];



T:=Factorization(Resultant(Lz,Ly,g));

res1g:=T[7,1];

T:=Factorization(Resultant(Ly,Lt,g));

res2g:=T[4,1];

T:=Factorization(Resultant(Ly,Lg,g));

res3g:=T[4,1];


T:=Factorization(Resultant(res1g,res2g,y));

res1y:=T[7,1];

T:=Factorization(Resultant(res1g,res3g,y));

res2y:=T[7,1];


T:=Factorization(Resultant(res1y,res2y,t));

rest:=T[3,1]*T[4,1]*T[5,1];


PrintFile("6F_Pari.gp","\{rest=");
PrintFile("6F_Pari.gp",rest);
PrintFile("6F_Pari.gp",";}\n\n");

PrintFile("6F_Pari.gp","\{res1y=");
PrintFile("6F_Pari.gp",res1y);
PrintFile("6F_Pari.gp",";}\n\n");

/* GP computations find no stationary points */

/* No need to check a = 1, g = 1: the function is zero */

/* g = 1: the function is zero */

/* We are left with the check on the interior */


L:=(1+y*z)*(1-2*y*z*t*g+(y*z*t)^2)*(1-y*z*t*a)*(1-z)*(1+2*z*t*g+(z*t)^2)*(1+z*t*a); /* times 2*sqrt(1-g^2) */


Tz:=Factorization(Derivative(L,z));

Ty:=Factorization(Derivative(L,y));

Tt:=Factorization(Derivative(L,t));

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g)*(1-g^2)-L*g);


Lz:=Tz[1,1];

Ly:=Ty[5,1];

Lt:=Tt[4,1];

La:=Ta[5,1];

Lg:=Tg[5,1];



T:=Factorization(Resultant(Lz,La,a));

res1a:=T[5,1];

T:=Factorization(Resultant(Ly,La,a));

res2a:=T[2,1];

T:=Factorization(Resultant(Lt,La,a));

res3a:=T[5,1];

T:=Factorization(Resultant(Lg,La,a));

res4a:=T[1,1];




T:=Factorization(Resultant(res1a,res2a,g));

res1g:=T[8,1];

T:=Factorization(Resultant(res3a,res2a,g));

res2g:=T[7,1];

T:=Factorization(Resultant(res4a,res2a,g));

res3g:=T[7,1];


T:=Factorization(Resultant(res1g,res2g,t));

res1t:=T[6,1];

T:=Factorization(Resultant(res1g,res3g,t));

res2ta:=T[5,1];

res2tb:=T[6,1];

/* WE HAVE res1t=res2ta:  this means we have to check just z= (1/4-3/4*y)/(y^2)   */

A:=-Coefficient(res1t,z,0);
B:=Coefficient(res1t,z,1);
S:=0;
N:=res1g;
for j in [0 .. Degree(N,z)] do
   S:=S+Coefficient(N,z,j)*A^j*B^(Degree(N,z)-j);
end for;
T:=Factorization(S);


res1Sza:=T[3,1];

res1Szb:=T[4,1];

res1Szc:=T[5,1];


A:=-Coefficient(res1t,z,0);
B:=Coefficient(res1t,z,1);
S:=0;
N:=res2g;
for j in [0 .. Degree(N,z)] do
   S:=S+Coefficient(N,z,j)*A^j*B^(Degree(N,z)-j);
end for;
T:=Factorization(S);


res2Szb:=T[3,1];

res2Szc:=T[4,1];

/* We have res1Szb = res2Szb and res1Szc = res2Szc.
   When res1Szb = 0 we get t = y^2/(3/4*y-1/4): this means z*t = -1, which is not possible */

/* When res1Szc = 0 we get t = y^2/(-3/4*y+1/4): this means z*t = 1, which is a boundary condition examined before */


/* We have concluded the study of the interior.

   Thus, we have proved that the function is <= 64/27 */
