/* Study of inequality 2H

   - - + +'    <= 2
     + - -'

*/

  Q:=RationalField();
  R<x,y,z,t,g>:=PolynomialRing(Q,5);

  L:= (1+x)*(1+x*y)*(1-x*y*z)*(1-2*g*x*y*z*t+(x*y*z*t)^2)*(1-y)*(1+y*z)*(1+2*g*y*z*t+(y*z*t)^2);

/* INNER */
  Tx:= Factorization(Derivative(L,x));
  Ty:= Factorization(Derivative(L,y));
  Tz:= Factorization(Derivative(L,z));
  Tt:= Factorization(Derivative(L,t));
  Tg:= Factorization(Derivative(L,g));
  Lx:= Tx[3,1];
  Ly:= Ty[2,1];
  Lz:= Tz[5,1];
  Lt:= Tt[8,1];
  Lg:= Tg[9,1];

  T:= Factorization(Resultant(Lx,Lg,g));
  /* no roots */

/* BORDER */
/* x = 0:
  (1-y)*(1+y*z)*(1+2*g*y*z*t+(y*z*t)^2) maximized at z = t = g = 1
  (1-y)*(1+y)^3 <= 1.69  */


/* x = 1:
  2*(1+y)*(1-y*z)*(1-2*g*y*z*t+(y*z*t)^2)*(1-y)*(1+y*z)*(1+2*g*y*z*t+(y*z*t)^2)  maximized for g=0
  2*(1-y^2)*(1-(y*z)^2)*(1+(y*z*t)^2)^2   maximized for t=1
  2*(1-y^2)*(1-(y*z)^4)*(1+(y*z)^2)  <= 2 (see 2H_Pari.gp)*/


/* y = 0:
  (1+x) <= 2  */


/* y = 1:
   function is 0
*/


/* z = 0:
  (1+x)*(1+x*y)*(1-y) <= 2
*/


/* z = 1:
  (1+x)*(1+x*y)*(1-x*y)*(1-2*g*x*y*t+(x*y*t)^2)*(1-y)*(1+y)*(1+2*g*y*t+(y*t)^2) */

  L:= (1+x)*(1+x*y)*(1-x*y)*(1-2*g*x*y*t+(x*y*t)^2)*(1-y)*(1+y)*(1+2*g*y*t+(y*t)^2);

  Tx:= Factorization(Derivative(L,x));
  Ty:= Factorization(Derivative(L,y));
  Tt:= Factorization(Derivative(L,t));
  Tg:= Factorization(Derivative(L,g));
  Lx:= Tx[4,1];
  Ly:= Ty[2,1];
  Lt:= Tt[8,1];
  Lg:= Tg[9,1];

  T:= Factorization(Resultant(Lx,Lg,g));
  res1g:= T[5,1];
  T:= Factorization(Resultant(Ly,Lg,g));
  res2g:= T[7,1];
  T:= Factorization(Resultant(Lt,Lg,g));
  /* No roots */


/* z = 1, t = 0:
  (1+x)*(1-(x*y)^2)*(1-y^2) <= 2 (see 2H_Pari.gp) */


/* z = 1, t = 1:
  (1+x)*(1+x*y)*(1-x*y)*(1-2*g*x*y+(x*y)^2)*(1-y)*(1+y)*(1+2*g*y+y^2) <= 2 (see 2H_Pari.gp) */


/* z = 1, g = 0:
  (1+x)*(1+x*y)*(1-x*y)*(1+(x*y*t)^2)*(1-y)*(1+y)*(1+(y*t)^2) maximized for t = 1
  (1+x)*(1-(x*y)^4)*(1-y^4) <= 2 */


/* z = 1, g = 1:
  (1+x)*(1+x*y)*(1-x*y)*(1-x*y*t)^2*(1-y)*(1+y)*(1+y*t)^2 <= 2 (see 2H_Pari.gp) */
/* case z = 1 ends here */


/* g = 0:
  (1+x)*(1+x*y)*(1-x*y*z)*(1+(x*y*z*t)^2)*(1-y)*(1+y*z)*(1+(y*z*t)^2) */


  L:=(1+x)*(1+x*y)*(1-x*y*z)*(1+(x*y*z*t)^2)*(1-y)*(1+y*z)*(1+(y*z*t)^2);
  Tt:= Factorization(Derivative(L,t));
  /* no roots */
/* case g = 1 ends here */


/* g = 1:
  (1+x)*(1+x*y)*(1-x*y*z)*(1-x*y*z*t)^2*(1-y)*(1+y*z)*(1+y*z*t)^2 */

  L:= (1+x)*(1+x*y)*(1-x*y*z)*(1-x*y*z*t)^2*(1-y)*(1+y*z)*(1+y*z*t)^2;

  Tx:= Factorization(Derivative(L,x));
  Ty:= Factorization(Derivative(L,y));
  Tz:= Factorization(Derivative(L,z));
  Tt:= Factorization(Derivative(L,t));
  Lx:= Tx[5,1];
  Ly:= Ty[4,1];
  Lz:= Tz[7,1];
  Lt:= Tt[10,1];

  T:= Factorization(Resultant(Lx,Lt,t));
  res1t:= T[4,1];
  T:= Factorization(Resultant(Ly,Lt,t));
  res2t:= T[5,1];
  T:= Factorization(Resultant(Lz,Lt,t));
  res3t:= T[5,1];

  T:= Factorization(Resultant(res1t,res3t,z));
  res1z:= T[4,1];
  T:= Factorization(Resultant(res2t,res3t,z));
  res2z:= T[4,1];

  T:= Factorization(Resultant(res1z,res2z,y));
  res1x:= T[3,1];

  PrintFile("2H_Pari.gp","\{res1x=");
  PrintFile("2H_Pari.gp",res1x);
  PrintFile("2H_Pari.gp",";}\n\n");

  PrintFile("2H_Pari.gp","\{res1z=");
  PrintFile("2H_Pari.gp",res1z);
  PrintFile("2H_Pari.gp",";}\n\n");

  PrintFile("2H_Pari.gp","\{res1t=");
  PrintFile("2H_Pari.gp",res1t);
  PrintFile("2H_Pari.gp",";}\n\n");

  PrintFile("2H_Pari.gp","\{Lt=");
  PrintFile("2H_Pari.gp",Lt);
  PrintFile("2H_Pari.gp",";}\n\n");

  /* GP computations shows that there are no stationary points */

/* This ends our study: the function is <= 2 */
