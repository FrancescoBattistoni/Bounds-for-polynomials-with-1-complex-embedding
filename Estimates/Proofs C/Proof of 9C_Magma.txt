/* Study of Inequality 9C */

Q:=RationalField();
R<x,y,z,t,a,g>:=PolynomialRing(Q,6);

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1-2*x*y*z*t*a*g+(x*y*z*t*a)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*a*g+(y*z*t*a)^2);


/* We study the 0-boundaries */

/* x = 0: the function becomes

   (1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*a*g+(y*z*t*a)^2). We maximize at a = g = 1 obtaining

   (1+y)*(1-y*z)*(1-y*z*t)*(1+y*z*t)^2 <=

   (1+y) * (1-y*z)*(1+y*z)  * (1-y*z*t)*(1+y*z*t).

   So at x = 0 the function is <= 2 */


/* y = 0:  we are left with (1+x) <= 2 */

/* z = 0: the function becomes

   (1+x)*(1-x*y)*(1+y). Apply 9A: this is <= 2.  */


/* t = 0: the function becomes

  (1+x)*(1-x*y)*(1+x*y*z)*(1+y)*(1-y*z)  =    (1+x)*(1-x*y)*(1+y)  *   (1+x*y*z)*(1-y*z)   <=   2 * 1 = 2  */

/* a = 0:

   (1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1+y)*(1-y*z)*(1-y*z*t) =   (1+x)*(1-x*y)*(1+y)  *   (1+x*y*z)*(1-y*z) *  (1+x*y*z*t)*(1-y*z*t)   <=   2 * 1 * 1 = 2   (apply 9A to first factor and 5A to the others)   */


/* g = 0:

   (1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1+(x*y*z*t*a)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+(y*z*t*a)^2). Maximized at a = 1:

   (1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1+(x*y*z*t)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+(y*z*t)^2).

   This is factored as

   (1+x)*(1-x*y)*(1+y)  * (1-y*z)*(1+x*y*z)*(1+(x*y*z*t)^2)  *  (1-y*z*t)*(1+x*y*z*t)*(1+(y*z*t)^2)  <=

    2  (apply 9A)      *       (1-(y*z)^4)                   *   (1-(y*z*t)^4)       <=  2*1*1 = 2                    */



/* Let us start the study of the 1-boundaries */


/* x = 1:

  2*(1-y)*(1+y*z)*(1+y*z*t)*(1-2*y*z*t*a*g+(y*z*t*a)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*a*g+(y*z*t*a)^2)

  This is maximized at g = 0, which we already studied. So it is <=  2  */


/* y = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-2*x*z*t*a*g+(x*z*t*a)^2)*(1-z)*(1-z*t)*(1+2*z*t*a*g+(z*t*a)^2)

*/

L:=2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-2*x*z*t*a*g+(x*z*t*a)^2)*(1-z)*(1-z*t)*(1+2*z*t*a*g+(z*t*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));

La:=Ta[9,1];

Lg:=Tg[10,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no zeros in the interior */


/* y = 1, z = 1: the function is zero */

/* y = 1, t = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-2*x*z*a*g+(x*z*a)^2)*(1-z)*(1-z)*(1+2*z*a*g+(z*a)^2)  */

L:=2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-2*x*z*a*g+(x*z*a)^2)*(1-z)*(1-z)*(1+2*z*a*g+(z*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));


La:=Ta[6,1];

Lg:=Tg[7,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no zeros in the interior */


/* y = 1, t = 1, a = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-2*x*z*g+(x*z)^2)*(1-z)*(1-z)*(1+2*z*g+z^2) */

L:= 2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-2*x*z*g+(x*z)^2)*(1-z)*(1-z)*(1+2*z*g+z^2);

Tx:=Factorization(Derivative(L,x));

Tz:=Factorization(Derivative(L,z));

Tg:=Factorization(Derivative(L,g));

Lx:= Tx[4,1];

Lz:= Tz[5,1];

Lg:= Tg[6,1];


T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[3,1];

T:=Factorization(Resultant(Lz,Lg,g));

res2g:=T[5,1];

T:=Factorization(Resultant(res1g,res2g,x));

/* The only non trivial factor is

   z^6 + 2/5*z^5 + 17/40*z^4 - 1/20*z^3 + 3/20*z^2 + 1/20*z + 1/20 =  z^6 + 2/5*z^5 + 17/40*z^4 + 3/20*z^2 + 1/20  +1/20*z*(1-z^2) which is non negative over [0,1] */


/* y = 1, t = 1, a = 1, g = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-x*z)^2*(1-z)*(1-z)*(1+z)^2  =

   2 * (1-x^2) * (1-(x*z)^2)^2 * (1-z^2)^2  <=  2 */


/* y = 1, t = 1, g = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-x*z*a)^2*(1-z)*(1-z)*(1+z*a)^2  */

L:=2*(1+x)*(1-x)*(1+x*z)*(1+x*z)*(1-x*z*a)^2*(1-z)*(1-z)*(1+z*a)^2;


Tz:=Factorization(Derivative(L,z));

Ta:=Factorization(Derivative(L,a));


Lz:= Tz[7,1];

La:= Ta[8,1];



T:=Factorization(Resultant(Lz,La,a));

/* This has no zeros in the interior */


/* y = 1, a = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-2*x*z*t*g+(x*z*t)^2)*(1-z)*(1-z*t)*(1+2*z*t*g+(z*t)^2)  */

L:=2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-2*x*z*t*g+(x*z*t)^2)*(1-z)*(1-z*t)*(1+2*z*t*g+(z*t)^2);

Tx:=Factorization(Derivative(L,x));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g));



Lx:= Tx[4,1];

Lz:= Tz[3,1];

Lt:= Tt[6,1];

Lg:=Tg[9,1];


T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[4,1];

T:=Factorization(Resultant(Lz,Lg,g));

res2g:=T[6,1];

T:=Factorization(Resultant(Lt,Lg,g));

res3g:=T[6,1];



T:=Factorization(Resultant(res1g,res3g,x));

res1x:=T[4,1];

T:=Factorization(Resultant(res2g,res3g,x));

res2x:=T[4,1];


T:=Factorization(Resultant(res1x,res2x,z));

resz:=T[5,1];
/* The other non trivial factor is discarded since it is t^6 - 4/3*t^5 + 52/9*t^4 + 10/9*t^3 + 88/9*t^2 + 44/3*t + 5 = t^6 + 52/9*t^4 + 10/9*t^3 + 88/9*t^2 + 44/3*t + ( 5 -4/3*t^5) which is non negative over [0,1] */

/* resz has no roots in [0,1] */


/* y = 1, a = 1, g = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-x*z*t)^2*(1-z)*(1-z*t)*(1+z*t)^2  */


L:=2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-x*z*t)^2*(1-z)*(1-z*t)*(1+z*t)^2;

Tx:=Factorization(Derivative(L,x));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));



Lx:= Tx[5,1];

Lz:= Tz[5,1];

Lt:= Tt[8,1];



T:=Factorization(Resultant(Lx,Lt,x));

res1x:=T[5,1];

T:=Factorization(Resultant(Lz,Lt,x));

res2x:=T[5,1];



T:=Factorization(Resultant(res1x,res2x,z));

/* One of the factors is t-1/3. But res2x at t=1/3 becomes (z-1)^2 */

resz:=T[5,1]*T[6,1];

PrintFile("9C.gp","\{resz=");
PrintFile("9C.gp",resz);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res2x=");
PrintFile("9C.gp",res2x);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{Lt=");
PrintFile("9C.gp",Lt);
PrintFile("9C.gp",";}\n\n");

/* GP computations show that we have no stationary points giving values beyond 2 */


/* y = 1, g = 1:

   2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-x*z*t*a)^2*(1-z)*(1-z*t)*(1+z*t*a)^2   */

L:= 2*(1+x)*(1-x)*(1+x*z)*(1+x*z*t)*(1-x*z*t*a)^2*(1-z)*(1-z*t)*(1+z*t*a)^2;


Tt:=Factorization(Derivative(L,t));

Ta:=Factorization(Derivative(L,a));


Lt:=Tt[8,1];

La:= Ta[11,1];


T:=Factorization(Resultant(Lt,La,a));

/* This has no zeros in the interior */

/* y = 1 is concluded */



/* z = 1: */


L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y*t)*(1-2*x*y*t*a*g+(x*y*t*a)^2)*(1+y)*(1-y)*(1-y*t)*(1+2*y*t*a*g+(y*t*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));


La:=Ta[10,1];

Lg:=Tg[11,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no zeros in the interior */


/* z = 1, t = 1: */

L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y)*(1-2*x*y*a*g+(x*y*a)^2)*(1+y)*(1-y)*(1-y)*(1+2*y*a*g+(y*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));


La:=Ta[7,1];

Lg:=Tg[8,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no zeros in the interior */

/* z = 1, t = 1, a = 1 */

L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y)*(1-2*x*y*g+(x*y)^2)*(1+y)*(1-y)*(1-y)*(1+2*y*g+y^2);

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Tg:=Factorization(Derivative(L,g));



Lx:= Tx[5,1];

Ly:= Ty[4,1];

Lg:= Tg[7,1];



T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[3,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[5,1];



T:=Factorization(Resultant(res1g,res2g,x));

/* The non-trivial factor has no roots in [0,1] */


/* z = 1, t = 1, a = 1, g = 1:

   (1+x)*(1-x*y)*(1+x*y)*(1+x*y)*(1-x*y)^2*(1+y)*(1-y)*(1-y)*(1+y)^2 =


   (1+x)*(1-x*y)*(1+y)   *   (1+x*y)*(1+x*y)*(1-x*y)^2  *  (1+y)*(1-y)*(1-y)*(1+y)    <=   2 * 1 * 1 = 2  */


/* z = 1, t = 1, g = 1 */


L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y)*(1-x*y*a)^2*(1+y)*(1-y)*(1-y)*(1+y*a)^2;

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Ta:=Factorization(Derivative(L,a));



Lx:= Tx[6,1];

Ly:= Ty[6,1];

La:= Ta[9,1];



T:=Factorization(Resultant(Lx,La,a));

res1a:=T[3,1];

T:=Factorization(Resultant(Ly,La,a));

res2a:=T[4,1];



T:=Factorization(Resultant(res1a,res2a,x));

/* No roots on [0,1], the only non-trivial factor is positive on the interval */



/* z = 1, a = 1 */


L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y*t)*(1-2*x*y*t*g+(x*y*t)^2)*(1+y)*(1-y)*(1-y*t)*(1+2*y*t*g+(y*t)^2);

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g));



Lx:= Tx[5,1];

Ly:= Ty[2,1];

Lt:= Tt[7,1];

Lg:=Tg[10,1];


T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[4,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[6,1];

T:=Factorization(Resultant(Lt,Lg,g));

res3g:=T[6,1];



T:=Factorization(Resultant(res1g,res3g,x));

res1x:=T[4,1];

T:=Factorization(Resultant(res2g,res3g,x));

res2x:=T[4,1];


T:=Factorization(Resultant(res1x,res2x,y));

resy:=T[4,1]*T[7,1];


PrintFile("9C.gp","\{resy=");
PrintFile("9C.gp",resy);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res2x=");
PrintFile("9C.gp",res2x);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res3g=");
PrintFile("9C.gp",res3g);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{Lt=");
PrintFile("9C.gp",Lt);
PrintFile("9C.gp",";}\n\n");


/* Computations in GP show that no stationary points give values beyond 2 */


/* z = 1, a = 1, g = 1:

   (1+x)*(1-x*y)*(1+x*y)*(1+x*y*t)*(1-x*y*t)^2*(1+y)*(1-y)*(1-y*t)*(1+y*t)^2

   (1-x*y)*(1+x*y)*(1+x*y*t)*(1-x*y*t)*(1+y)*(1-y)*(1-y*t)*(1+y*t)

   (1+x)*(1-x*y*t)*(1+y*t) *   (1-x*y)*(1+x*y)*          (1+x*y*t)*(1-x*y*t)*           (1+y)*(1-y)*        (1-y*t)*(1+y*t)   <=   2 (apply 9A) * 1 * 1 * 1 * 1 = 2  */


/* z = 1, g = 1 */

L:=(1+x)*(1-x*y)*(1+x*y)*(1+x*y*t)*(1-x*y*t*a)^2*(1+y)*(1-y)*(1-y*t)*(1+y*t*a)^2;

Tt:=Factorization(Derivative(L,t));

Ta:=Factorization(Derivative(L,a));


Lt:=Tt[9,1];

La:= Ta[12,1];


T:=Factorization(Resultant(Lt,La,a));

/* This has no zeros in the interior */





/* t = 1:  */


L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z)*(1-2*x*y*z*a*g+(x*y*z*a)^2)*(1+y)*(1-y*z)*(1-y*z)*(1+2*y*z*a*g+(y*z*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));


La:=Ta[8,1];

Lg:=Tg[9,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no zeros in the interior */


/* t = 1, a = 1 :*/

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z)*(1-2*x*y*z*g+(x*y*z)^2)*(1+y)*(1-y*z)*(1-y*z)*(1+2*y*z*g+(y*z)^2);

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tg:=Factorization(Derivative(L,g));



Lx:= Tx[5,1];

Ly:= Ty[4,1];

Lz:= Tz[7,1];

Lg:=Tg[8,1];


T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[4,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[6,1];

T:=Factorization(Resultant(Lz,Lg,g));

res3g:=T[6,1];



T:=Factorization(Resultant(res1g,res3g,x));

res1x:=T[4,1];

T:=Factorization(Resultant(res2g,res3g,x));

res2x:=T[4,1];

/* This has no roots over [0,1] */


/* t = 1, a = 1, g = 1

   (1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z)*(1-x*y*z)^2*(1+y)*(1-y*z)*(1-y*z)*(1+y*z)^2 =


   (1+x)*(1-x*y)*(1+y)   *   (1+x*y*z)*(1+x*y*z)*(1-x*y*z)^2   *   (1-y*z)*(1-y*z)*(1+y*z)^2  <=  2 (apply 9A) * 1 * 1 = 2 */


/* t = 1, g = 1: */

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z)*(1-x*y*z*a)^2*(1+y)*(1-y*z)*(1-y*z)*(1+y*z*a)^2;


Tz:=Factorization(Derivative(L,z));

Ta:=Factorization(Derivative(L,a));


Lz:= Tz[9,1];

La:=Ta[10,1];



T:=Factorization(Resultant(Lz,La,a));

/* This has no roots */


/* a = 1: */

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1-2*x*y*z*t*g+(x*y*z*t)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*g+(y*z*t)^2);

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));

Tg:=Factorization(Derivative(L,g));

Lx:= Tx[5,1];

Ly:= Ty[2,1];

Lz:= Tz[5,1];

Lt:= Tt[8,1];

Lg:=Tg[11,1];


T:=Factorization(Resultant(Lx,Lg,g));

res1g:=T[5,1];

T:=Factorization(Resultant(Ly,Lg,g));

res2g:=T[7,1];

T:=Factorization(Resultant(Lz,Lg,g));

res3g:=T[7,1];

T:=Factorization(Resultant(Lt,Lg,g));

res4g:=T[7,1];



T:=Factorization(Resultant(res1g,res4g,x));

res1x:=T[5,1];


T:=Factorization(Resultant(res2g,res4g,x));

res2x:=T[5,1];


T:=Factorization(Resultant(res3g,res4g,x));

res3x:=T[5,1];



T:=Factorization(Resultant(res1x,res3x,y));

res1y:=T[5,1]*T[6,1];


T:=Factorization(Resultant(res2x,res3x,y));

res2y:=T[6,1];


T:=Factorization(Resultant(res1y,res2y,z));

resz:=T[7,1]*T[10,1]*T[11,1]*T[12,1];

PrintFile("9C.gp","\{resz=");
PrintFile("9C.gp",resz);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res2y=");
PrintFile("9C.gp",res2y);
PrintFile("9C.gp",";}\n\n");


/* The only roots of resz is 12/13 (coming from T[7,1]): substitution in res2y gives no acceptable roots */

/* a = 1, g = 1 */

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1-x*y*z*t)^2*(1+y)*(1-y*z)*(1-y*z*t)*(1+y*z*t)^2;

Tx:=Factorization(Derivative(L,x));

Ty:=Factorization(Derivative(L,y));

Tz:=Factorization(Derivative(L,z));

Tt:=Factorization(Derivative(L,t));


Lx:= Tx[6,1];

Ly:= Ty[4,1];

Lz:= Tz[7,1];

Lt:= Tt[10,1];




T:=Factorization(Resultant(Lx,Lt,x));

res1x:=T[6,1];

T:=Factorization(Resultant(Ly,Lt,x));

res2x:=T[6,1];

T:=Factorization(Resultant(Lz,Lt,x));

res3x:=T[6,1];




T:=Factorization(Resultant(res1x,res3x,y));

res1ya:=T[4,1];

res1yb:=T[6,1]*T[7,1];


T:=Factorization(Resultant(res2x,res3x,y));

res2ya:=T[4,1];

res2yb:=T[7,1];


/* res1ya = res2ya = t-1/3. Evaluation of res3x at t = 1/3 gives (y*z-1)^2, which gives not roots in the interior */


T:=Factorization(Resultant(res1yb,res2yb,z));

resz:=T[8,1]*T[9,1]*T[10,1]*T[11,1]*T[12,1];


PrintFile("9C.gp","\{resz=");
PrintFile("9C.gp",resz);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res2yb=");
PrintFile("9C.gp",res2yb);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{res3x=");
PrintFile("9C.gp",res3x);
PrintFile("9C.gp",";}\n\n");

PrintFile("9C.gp","\{Lt=");
PrintFile("9C.gp",Lt);
PrintFile("9C.gp",";}\n\n");


/* GP computations show there are no stationary points giving values of the function beyond 2 */

/* This concludes a = 1 */



/* g = 1: */

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1-2*x*y*z*t*a+(x*y*z*t*a)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*a+(y*z*t*a)^2);


Tt:=Factorization(Derivative(L,t));

Ta:=Factorization(Derivative(L,a));


Lt:= Tt[10,1];

La:=Ta[13,1];


T:=Factorization(Resultant(Lt,La,a));

/* This has no roots in the interior */

/* This concludes the case g = 1 */



/* We are done with the 1-boundaries, and so with the boundaries: we are left with analysis of the interior */

L:=(1+x)*(1-x*y)*(1+x*y*z)*(1+x*y*z*t)*(1-2*x*y*z*t*a*g+(x*y*z*t*a)^2)*(1+y)*(1-y*z)*(1-y*z*t)*(1+2*y*z*t*a*g+(y*z*t*a)^2);

Ta:=Factorization(Derivative(L,a));

Tg:=Factorization(Derivative(L,g));

La:=Ta[11,1];

Lg:=Tg[12,1];


T:=Factorization(Resultant(La,Lg,g));

/* This has no roots in the interior */


/* HENCE THIS FUNCTION IS <= 2 */
